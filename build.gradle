plugins {
    id 'java'
}

group = 'com.logicmonitor.aws.lambdas'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.4.0'
    implementation platform('software.amazon.awssdk:bom:2.33.5')
    implementation 'software.amazon.awssdk:iam'
    implementation 'org.slf4j:slf4j-api:2.0.17'
    implementation 'com.google.code.gson:gson:2.13.1'

    constraints {
        implementation ('io.netty:netty-codec-http:4.2.6.Final') {
            because 'lower version pulled had vulnerability affecting this application'
        }
        implementation ('io.netty:netty-codec-http2:4.2.6.Final') {
            because 'lower version pulled had vulnerability affecting this application'
        }
        implementation ('io.netty:netty-handler:4.2.6.Final') {
            because 'lower version pulled had vulnerability affecting this application'
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(file('.java-version').text.split('\\.').first())
    }
}

test {
    useJUnitPlatform()
}

tasks.register('buildZip', Zip) {
    from compileJava
    from processResources
    into('lib') {
        from jar
        from configurations.runtimeClasspath
    }
}